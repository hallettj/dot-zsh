# Load completions provided by fzf nix package
if [[ -f "$HOME/.nix-profile/share/fzf/completion.zsh" ]]; then
  source "$HOME/.nix-profile/share/fzf/completion.zsh"
fi

# Load key bindings provided by Debian package
if [[ -f "/usr/share/doc/fzf/examples/key-bindings.zsh" ]]; then
  source "/usr/share/doc/fzf/examples/key-bindings.zsh"
fi

# Load key bindings provided by fzf nix package
if [[ -f "$HOME/.nix-profile/share/fzf/key-bindings.zsh" ]]; then
  source "$HOME/.nix-profile/share/fzf/key-bindings.zsh"
fi

__fzf_edit_file() {
  local cmd="$1"
  local file="$(FZF_CTRL_T_COMMAND="$cmd" __fsel)"
  local ret=$?
  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
  if [[ -n "$file" ]]; then
    BUFFER="${EDITOR:-vim} $file"
    zle redisplay
    zle accept-line
  fi
  return $ret
}

# Use fzf to find a file under the working directory using `$EDITOR`
function fzf-edit-project-file() {
  __fzf_edit_file "fd ."
}
zle -N fzf-edit-project-file

# Use fzf to find a file under your home directory using `$EDITOR`
function fzf-edit-home-file() {
  __fzf_edit_file "fd . $HOME"
}
zle -N fzf-edit-home-file

function fzf-edit-home-file-include-hidden() {
  __fzf_edit_file "fd --hidden . $HOME"
}
zle -N fzf-edit-home-file-include-hidden

function fzf-goto-project-directory() {
  local top="$(git rev-parse --show-toplevel 2>/dev/null || true)"
  local dir="${top:-.}"
  FZF_ALT_C_COMMAND="fd --type directory . '$dir'" zle fzf-cd-widget
}
zle -N fzf-goto-project-directory

function fzf-goto-root-directory() {
  FZF_ALT_C_COMMAND="fd --type directory . /" zle fzf-cd-widget
}
zle -N fzf-goto-root-directory
